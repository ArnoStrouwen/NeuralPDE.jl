# 3-dimensional PDEs

the 3-dimensional PDE:
![3dpde](https://user-images.githubusercontent.com/12683885/90976452-d2c74400-e545-11ea-8361-288603d9ddbc.png)

with the initial and boundary conditions:

![boundary](https://user-images.githubusercontent.com/12683885/91332936-8c881400-e7d5-11ea-991a-39c9d61d4f24.png)

on the space and time domain:

![space](https://user-images.githubusercontent.com/12683885/90976622-3605a600-e547-11ea-837e-92330769f5ee.png)

with physics-informed neural networks.

```julia
using NeuralPDE, Flux, ModelingToolkit, GalacticOptim, Optim, DiffEqFlux
using Quadrature, Cuba
# 3D PDE
@parameters t x y
@variables u(..)
Dxx = Differential(x)^2
Dyy = Differential(y)^2
Dt = Differential(t)
t_min= 0.
t_max = 2.0
x_min = 0.
x_max = 2.
y_min = 0.
y_max = 2.

# 3D PDE
eq  = Dt(u(t,x,y)) ~ Dxx(u(t,x,y)) + Dyy(u(t,x,y))

analytic_sol_func(t,x,y) = exp(x+y)*cos(x+y+4t)
# Initial and boundary conditions
bcs = [u(t_min,x,y) ~ analytic_sol_func(t_min,x,y),
       u(t,x_min,y) ~ analytic_sol_func(t,x_min,y),
       u(t,x_max,y) ~ analytic_sol_func(t,x_max,y),
       u(t,x,y_min) ~ analytic_sol_func(t,x,y_min),
       u(t,x,y_max) ~ analytic_sol_func(t,x,y_max)]

# Space and time domains
domains = [t ∈ IntervalDomain(t_min,t_max),
           x ∈ IntervalDomain(x_min,x_max),
           y ∈ IntervalDomain(y_min,y_max)]

# Neural network
chain = FastChain(FastDense(3,16,Flux.σ),FastDense(16,16,Flux.σ),FastDense(16,1))

quadrature_strategy = QuadratureTraining(quadrature_alg=CubaCuhre(),
                                         reltol = 1e-5, abstol = 1e-3,
                                         maxiters = 10)
discretization = PhysicsInformedNN(chain, quadrature_strategy)
pde_system = PDESystem(eq,bcs,domains,[t,x,y],[u])
prob = discretize(pde_system,discretization)

cb = function (p,l)
    println("Current loss is: $l")
    return false
end  
res = GalacticOptim.solve(prob,ADAM(0.1);cb=cb,maxiters=50)
phi = discretization.phi

using Plots
using Printf
ts,xs,ys = [domain.domain.lower:0.01:domain.domain.upper for domain in domains]

function plot_()
    # Animate
    anim = @animate for (i, t) in enumerate(0:0.01:t_max)
        @info "Animating frame $i..."
        u_real = reshape([analytic_sol_func(t,x,y) for x in xs for y in ys], (length(xs),length(ys)))
        u_predict = reshape([phi([t, x, y], res.minimizer)[1] for x in xs for y in ys], length(xs), length(ys))
        u_error = abs.(u_predict .- u_real)
        title = @sprintf("predict t = %.3f", t)
        p1 = plot(xs, ys, u_predict,st=:surface, label="", title=title)
        title = @sprintf("real")
        p2 = plot(xs, ys, u_real,st=:surface, label="", title=title)
        title = @sprintf("error")
        p3 = plot(xs, ys, u_error, st=:contourf,label="", title=title)
        plot(p1,p2,p3)
    end
    gif(anim,"3pde.gif", fps=15)
end

plot_()

for t_ in collect(0.1:0.2:t_max)
    # Space and time domains
    domains_ = [t ∈ IntervalDomain(t_min,t_),
               x ∈ IntervalDomain(x_min,x_max),
               y ∈ IntervalDomain(y_min,y_max)]

    pde_system = remake(pde_system; domain = domains_)
    discretization = remake(discretization; init_params = res.minimizer)
    prob_ = NeuralPDE.discretize(pde_system,discretization)
    maxiters =800
    res = GalacticOptim.solve(prob_,ADAM(0.1);cb=cb,maxiters=maxiters)
    maxiters -=50
    plot_()
end

prob = remake(prob; u0 = res.minimizer)
res = GalacticOptim.solve(prob,ADAM(0.1);cb=cb,maxiters=1000)

plot_()
```
![3pde](https://user-images.githubusercontent.com/12683885/107345726-eefb7500-6ad4-11eb-889d-ba0fac0f1674.gif)
